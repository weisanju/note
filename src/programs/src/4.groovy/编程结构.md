# Package names

包名与 Java 中的作用完全相同。

它们允许我们在没有任何冲突的情况下分离代码库。 

Groovy 类必须在类定义之前指定它们的包，否则假定为默认包。

```groovy
// defining a package named com.yoursite
package com.yoursite
```



要引用 com.yoursite.com 包中的某个类 Foo，您需要使用完全限定名称 com.yoursite.com.Foo，否则您可以使用 import 语句，如下所示。

# Imports

为了引用任何类，您需要对其包进行限定引用。 

Groovy 遵循 Java 允许 import 语句解析类引用的概念

```groovy
// importing the class MarkupBuilder
import groovy.xml.MarkupBuilder

// using the imported class to create an object
def xml = new MarkupBuilder()

assert xml != null
```

## Default imports

默认导入是 Groovy 语言默认提供的导入。

```groovy
import java.lang.*
import java.util.*
import java.io.*
import java.net.*
import groovy.lang.*
import groovy.util.*
import java.math.BigInteger
import java.math.BigDecimal
```

这样做是因为这些包中的类是最常用的。通过导入这些样板代码减少了。

## Simple import

简单导入

```groovy
// importing the class MarkupBuilder
import groovy.xml.MarkupBuilder

// using the imported class to create an object
def xml = new MarkupBuilder()

assert xml != null
```

## Star import

提供了一种使用 * 从包中导入所有类的特殊方法，即所谓的星形导入。

```groovy
import groovy.xml.*

def markupBuilder = new MarkupBuilder()

assert markupBuilder != null

assert new StreamingMarkupBuilder() != null
```

## Static import

Groovy 的静态导入功能允许您引用导入的类，就像它们是您自己类中的静态方法一样：

```groovy
import static Boolean.FALSE

assert !FALSE //use directly, without Boolean prefix!
```

## 静态导入别名

带有 as 关键字的静态导入为命名空间问题提供了一种优雅的解决方案。

假设您想使用其 getInstance() 方法获取 Calendar 实例。

这是一个静态方法，所以我们可以使用静态导入。

```groovy
import static Calendar.getInstance as now

assert now().class == Calendar.getInstance().class
```

## Static star import

```groovy
import static java.lang.Math.*

assert sin(0) == 0.0
assert cos(0) == 1.0
```

## Import aliasing

导入别名

使用类型别名，我们可以使用我们选择的名称来引用完全限定的类名。

这可以像以前一样使用 as 关键字来完成。

例如，我们可以将 java.sql.Date 作为 SQLDate 导入并在与 java.util.Date 相同的文件中使用它，而不必使用任一类的完全限定名称：

```groovy
import java.util.Date
import java.sql.Date as SQLDate

Date utilDate = new Date(1000L)
SQLDate sqlDate = new SQLDate(1000L)

assert utilDate instanceof java.util.Date
assert sqlDate instanceof java.sql.Date
```

# Scripts versus classes

脚本与类

## public static void main vs script

Groovy 支持脚本和类。

```groovy
class Main {                                    
    static void main(String... args) {          
        println 'Groovy world!'                 
    }
}
```

这是您会发现来自 Java 的典型代码，其中代码必须嵌入到一个类中才能执行。 

```groovy
println 'Groovy world!'
```



一个脚本可以被认为是一个类而不需要声明它，但有一些区别。

## Script class

脚本总是被编译成一个类。 

Groovy 编译器将为您编译该类，并将脚本主体复制到 run 方法中。

因此，前面的示例被编译为如下所示：

```groovy
import org.codehaus.groovy.runtime.InvokerHelper
class Main extends Script {                     
    def run() {                                 
        println 'Groovy world!'                 
    }
    static void main(String[] args) {           
        InvokerHelper.runScript(Main, args)     
    }
}
```

* Main 类扩展了 groovy.lang.Script 类
* groovy.lang.Script 需要一个返回值的 run 方法
* 脚本主体进入 run 方法
* main方法是自动生成的
* 并将脚本的执行委托给 run 方法

如果脚本在文件中，则使用文件的基本名称来确定生成的脚本类的名称。

在这个例子中，如果文件名是 Main.groovy，那么脚本类就是 Main。



## Methods

```groovy
int fib(int n) {
    n < 2 ? 1 : fib(n-1) + fib(n-2)
}
assert fib(10)==89
```

您还可以混合使用方法和代码。

生成的脚本类会将所有的方法携带到脚本类中，并将所有的脚本体组装到run方法中：

```groovy
println 'Hello'                                 

int power(int n) { 2**n }                       

println "2^6==${power(6)}"                      
```

```groovy
import org.codehaus.groovy.runtime.InvokerHelper
class Main extends Script {
    int power(int n) { 2** n}                   
    def run() {
        println 'Hello'                         
        println "2^6==${power(6)}"              
    }
    static void main(String[] args) {
        InvokerHelper.runScript(Main, args)
    }
}
```

## Variables

脚本中的变量不需要类型定义。

这意味着这个脚本：

```groovy
int x = 1
int y = 2
assert x+y == 3
```

will behave the same as:

```groovy
x = 1
y = 2
assert x+y == 3
```

但是，两者之间存在语义差异：

如果变量如第一个示例中那样声明，则它是局部变量。

它将在编译器将生成的 run 方法中声明，并且在脚本主体之外不可见。

特别是，这样的变量在脚本的其他方法中是不可见的



如果变量未声明，它会进入脚本绑定。

绑定从方法中可见，如果您使用脚本与应用程序交互并需要在脚本和应用程序之间共享数据，则该绑定尤其重要。

使变量对所有方法可见的另一种方法是使用 @Field 注释。

这样注解的变量会成为生成的脚本类的一个字段，对于局部变量，访问不会涉及到脚本绑定。

虽然不推荐，但如果您有一个与绑定变量同名的局部变量或脚本字段，您可以使用 binding.varName 来访问绑定变量。






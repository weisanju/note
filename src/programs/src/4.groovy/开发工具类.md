# IO

## 读文件

**读文件**

```groovy
new File(baseDir, 'haiku.txt').eachLine { line ->
    println line
}
```

**行号与line**

```groovy
new File(baseDir, 'haiku.txt').eachLine { line, nb ->
    println "Line $nb: $line"
}
```

**自定义reader**

> 会自动关闭流

```groovy
def count = 0, MAXSIZE = 3
new File(baseDir,"haiku.txt").withReader { reader ->
    while (reader.readLine()) {
        if (++count > MAXSIZE) {
            throw new RuntimeException('Haiku should only have 3 verses')
        }
    }
}
```

**转list**

```sh
def list = new File(baseDir, 'haiku.txt').collect {it}
```

或者您甚至可以利用 as 运算符将文件的内容放入行数组：

```groovy
def array = new File(baseDir, 'haiku.txt') as String[]
```

**转换字节数组**

```groovy
byte[] contents = file.bytes
```

**获取输入流**

```groovy
def is = new File(baseDir,'haiku.txt').newInputStream()
// do something ...
is.close()
```

**自动关闭流**

```groovy
new File(baseDir,'haiku.txt').withInputStream { stream ->
    // do something ...
}
```

## 写文件

**闭包写文件**

```groovy
new File(baseDir,'haiku.txt').withWriter('utf-8') { writer ->
    writer.writeLine 'Into the ancient pond'
    writer.writeLine 'A frog jumps'
    writer.writeLine 'Water’s sound!'
}
```

**重定向符写文件**

```groovy
new File(baseDir,'haiku.txt') << '''Into the ancient pond
A frog jumps
Water’s sound!'''
```

**直接写字节数组**

```sh
file.bytes = [66,22,11]
```

直接使用 **outputstream**

```groovy
def os = new File(baseDir,'data.bin').newOutputStream()
// do something ...
os.close()
```

闭包使用**outputstream**

```sh
new File(baseDir,'data.bin').withOutputStream { stream ->
    // do something ...
}
```

## 遍历文件树

```groovy
dir.eachFile { file ->                      
    println file.name
}
dir.eachFileMatch(~/.*\.txt/) { file ->     
    println file.name
}
//递归
dir.eachFileRecurse { file ->                      
    println file.name
}

dir.eachFileRecurse(FileType.FILES) { file ->      
    println file.name
}
```

**手动控制递归**

```groovy
dir.traverse { file ->
    if (file.directory && file.name=='bin') {
        FileVisitResult.TERMINATE                   
    } else {
        println file.name
        FileVisitResult.CONTINUE                    
    }

}
```

## 数据和对象

在Java中很少使用 java.io.DataOutputStream` and `java.io.DataInputStream

```groovy
boolean b = true
String message = 'Hello from Groovy'
// Serialize data into a file
file.withDataOutputStream { out ->
    out.writeBoolean(b)
    out.writeUTF(message)
}
// ...
// Then read it back
file.withDataInputStream { input ->
    assert input.readBoolean() == b
    assert input.readUTF() == message
}
```



同样，如果要序列化的数据实现了 Serializable 接口，则可以继续使用对象输出流，

```groovy
Person p = new Person(name:'Bob', age:76)
// Serialize data into a file
file.withObjectOutputStream { out ->
    out.writeObject(p)
}
// ...
// Then read it back
file.withObjectInputStream { input ->
    def p2 = input.readObject()
    assert p2.name == p.name
    assert p2.age == p.age
}
```

**执行外部程序**

```groovy
// 执行外部命令
def process = "ls -l".execute()             
// 获取外部命令的输出，返回值类型 java.lang.Process
println "Found text ${process.text}"        
```

**处理 输入流**

```groovy
def process = "ls -l".execute()             
process.in.eachLine { line ->               
    println line                            
}
```

**windows命令**

```groovy
//这会直接报错，因为 dir是 windows shell自带命令，不能使用可执行路径执行，
def process = "dir".execute()
println "${process.text}"

def process = "cmd /c dir".execute()
println "${process.text}"
```

由于一些原生平台只为标准的输入输出流提供有限的缓冲区大小，未能及时写入输入流或读取子进程的输出流可能会导致子进程阻塞，甚至死锁

因此，Groovy 提供了一些额外的辅助方法，使流程的流处理更容易。

以下是如何从您的过程中 获取所有输出（包括错误流输出）：

```groovy
def p = "rm -f foo.tmp".execute([], tmpDir)
p.consumeProcessOutput()
p.waitFor()
```

也有使用 StringBuffer、InputStream、OutputStream 等的消耗进程输出的变体......有关完整列表，请阅读 java.lang.Process 的 GDK API

此外，这是一个 pipeTo 命令（映射到 | 以允许重载），它允许一个进程的输出流被馈送到另一个进程的输入流中。

```groovy
proc1 = 'ls'.execute()
proc2 = 'tr -d o'.execute()
proc3 = 'tr -d e'.execute()
proc4 = 'tr -d i'.execute()
proc1 | proc2 | proc3 | proc4
proc4.waitFor()
if (proc4.exitValue()) {
    println proc4.err.text
} else {
    println proc4.text
}
```



```groovy
def sout = new StringBuilder()
def serr = new StringBuilder()
proc2 = 'tr -d o'.execute()
proc3 = 'tr -d e'.execute()
proc4 = 'tr -d i'.execute()
proc4.consumeProcessOutput(sout, serr)
proc2 | proc3 | proc4
[proc2, proc3].each { it.consumeProcessErrorStream(serr) }
proc2.withWriter { writer ->
    writer << 'testfile.groovy'
}
proc4.waitForOrKill(1000)
println "Standard output: $sout"
println "Standard error: $serr"
```



# 集合

## Lists

### **定义与取值**

```groovy
#定义
def list = [5, 6, 7, 8]
# 取值
assert list.get(2) == 7
# 取值
assert list[2] == 7
# 类型比较
assert list instanceof java.util.List
# 空集合
def emptyList = []
# 集合大小
assert emptyList.size() == 0
# 添加集合
emptyList.add(5)
assert emptyList.size() == 1
```

### **ArrayList构造**

```groovy
def list1 = ['a', 'b', 'c']
//construct a new list, seeded with the same items as in list1
def list2 = new ArrayList<String>(list1)

assert list2 == list1 // == checks that each corresponding element is the same

// clone() can also be called
def list3 = list1.clone()
assert list3 == list1
```

### **getAt putAt**

```groovy
def list = [5, 6, 7, 8]
assert list.size() == 4
assert list.getClass() == ArrayList     // the specific kind of list being used

assert list[2] == 7                     // indexing starts at 0
assert list.getAt(2) == 7               // equivalent method to subscript operator []
assert list.get(2) == 7                 // alternative method

list[2] = 9
assert list == [5, 6, 9, 8,]           // trailing comma OK

list.putAt(2, 10)                       // equivalent method to [] when value being changed
assert list == [5, 6, 10, 8]
assert list.set(2, 11) == 10            // alternative method that returns old value
assert list == [5, 6, 11, 8]

assert ['a', 1, 'a', 'a', 2.5, 2.5f, 2.5d, 'hello', 7g, null, 9 as byte]
//objects can be of different types; duplicates allowed

assert [1, 2, 3, 4, 5][-1] == 5             // use negative indices to count from the end
assert [1, 2, 3, 4, 5][-2] == 4
assert [1, 2, 3, 4, 5].getAt(-2) == 4       // getAt() available with negative index...
try {
    [1, 2, 3, 4, 5].get(-2)                 // but negative index not allowed with get()
    assert false
} catch (e) {
    assert e instanceof IndexOutOfBoundsException
}
```

**迭代**

```groovy
[1, 2, 3].each {
    println "Item: $it" // `it` is an implicit parameter corresponding to the current element
}
['a', 'b', 'c'].eachWithIndex { it, i -> // `it` is the current element, while `i` is the index
    println "$i: $it"
}
```

### **转换**

```groovy
assert [1, 2, 3].collect { it * 2 } == [2, 4, 6]

// shortcut syntax instead of collect
assert [1, 2, 3]*.multiply(2) == [1, 2, 3].collect { it.multiply(2) }

def list = [0]
// it is possible to give `collect` the list which collects the elements
assert [1, 2, 3].collect(list) { it * 2 } == [0, 2, 4, 6]
assert list == [0, 2, 4, 6]
```

### **修改集合-搜索过滤**

```groovy
assert [1, 2, 3].find { it > 1 } == 2           // find 1st element matching criteria
assert [1, 2, 3].findAll { it > 1 } == [2, 3]   // find all elements matching critieria
assert ['a', 'b', 'c', 'd', 'e'].findIndexOf {      // find index of 1st element matching criteria
    it in ['c', 'e', 'g']
} == 2

assert ['a', 'b', 'c', 'd', 'c'].indexOf('c') == 2  // index returned
assert ['a', 'b', 'c', 'd', 'c'].indexOf('z') == -1 // index -1 means value not in list
assert ['a', 'b', 'c', 'd', 'c'].lastIndexOf('c') == 4

assert [1, 2, 3].every { it < 5 }               // returns true if all elements match the predicate
assert ![1, 2, 3].every { it < 3 }
assert [1, 2, 3].any { it > 2 }                 // returns true if any element matches the predicate
assert ![1, 2, 3].any { it > 3 }

assert [1, 2, 3, 4, 5, 6].sum() == 21                // sum anything with a plus() method
assert ['a', 'b', 'c', 'd', 'e'].sum {
    it == 'a' ? 1 : it == 'b' ? 2 : it == 'c' ? 3 : it == 'd' ? 4 : it == 'e' ? 5 : 0
    // custom value to use in sum
} == 15
assert ['a', 'b', 'c', 'd', 'e'].sum { ((char) it) - ((char) 'a') } == 10
assert ['a', 'b', 'c', 'd', 'e'].sum() == 'abcde'
assert [['a', 'b'], ['c', 'd']].sum() == ['a', 'b', 'c', 'd']

// an initial value can be provided
assert [].sum(1000) == 1000
assert [1, 2, 3].sum(1000) == 1006

assert [1, 2, 3].join('-') == '1-2-3'           // String joining
assert [1, 2, 3].inject('counting: ') {
    str, item -> str + item                     // reduce operation
} == 'counting: 123'
assert [1, 2, 3].inject(0) { count, item ->
    count + item
} == 6
```

### **最大值最小值**

```groovy
def list = [9, 4, 2, 10, 5]
assert list.max() == 10
assert list.min() == 2

// we can also compare single characters, as anything comparable
assert ['x', 'y', 'a', 'z'].min() == 'a'

// we can use a closure to specify the sorting behaviour
def list2 = ['abc', 'z', 'xyzuvw', 'Hello', '321']
assert list2.max { it.size() } == 'xyzuvw'
assert list2.min { it.size() } == 'z'
```

### **定义排序器**

```groovy
Comparator mc = { a, b -> a == b ? 0 : (a < b ? -1 : 1) }

def list = [7, 4, 9, -6, -1, 11, 2, 3, -9, 5, -13]
assert list.max(mc) == 11
assert list.min(mc) == -13

Comparator mc2 = { a, b -> a == b ? 0 : (Math.abs(a) < Math.abs(b)) ? -1 : 1 }


assert list.max(mc2) == -13
assert list.min(mc2) == -1

assert list.max { a, b -> a.equals(b) ? 0 : Math.abs(a) < Math.abs(b) ? -1 : 1 } == -13
assert list.min { a, b -> a.equals(b) ? 0 : Math.abs(a) < Math.abs(b) ? -1 : 1 } == -1
```

### **移除元素**

```groovy
def list = []
assert list.empty

list << 5
assert list.size() == 1

list << 7 << 'i' << 11
assert list == [5, 7, 'i', 11]

list << ['m', 'o']
assert list == [5, 7, 'i', 11, ['m', 'o']]

//first item in chain of << is target list
assert ([1, 2] << 3 << [4, 5] << 6) == [1, 2, 3, [4, 5], 6]

//using leftShift is equivalent to using <<
assert ([1, 2, 3] << 4) == ([1, 2, 3].leftShift(4))
```

### **集合加减与展开**

```groovy
# 连加
assert [1, 2] + 3 + [4, 5] + 6 == [1, 2, 3, 4, 5, 6]

//API连＋ equivalent to calling the `plus` method
assert [1, 2].plus(3).plus([4, 5]).plus(6) == [1, 2, 3, 4, 5, 6]
# +=，每次都会建一个新的集合
def a = [1, 2, 3]
a += 4      // creates a new list and assigns it to `a`
a += [5, 6]
assert a == [1, 2, 3, 4, 5, 6]

# 平铺符
assert [1, *[222, 333], 456] == [1, 222, 333, 456]
assert [*[1, 2, 3]] == [1, 2, 3]
# 展开符
assert [1, [2, 3, [4, 5], 6], 7, [8, 9]].flatten() == [1, 2, 3, 4, 5, 6, 7, 8, 9]

def list = [1, 2]
list.add(3)
list.addAll([5, 4])
assert list == [1, 2, 3, 5, 4]

list = [1, 2]
list.add(1, 3) // add 3 just before index 1
assert list == [1, 3, 2]

list.addAll(2, [5, 4]) //add [5,4] just before index 2
assert list == [1, 3, 5, 4, 2]

list = ['a', 'b', 'z', 'e', 'u', 'v', 'g']
list[8] = 'x' // the [] operator is growing the list as needed
// nulls inserted if required
assert list == ['a', 'b', 'z', 'e', 'u', 'v', 'g', null, 'x']
```

```groovy
assert ['a','b','c','b','b'] - 'c' == ['a','b','b','b']
assert ['a','b','c','b','b'] - 'b' == ['a','c']
assert ['a','b','c','b','b'] - ['b','c'] == ['a']

def list = [1,2,3,4,3,2,1]
list -= 3           // creates a new list by removing `3` from the original one
assert list == [1,2,4,2,1]
assert ( list -= [2,4] ) == [1,1]
```

```groovy
def list = ['a','b','c','d','e','f','b','b','a']
assert list.remove(2) == 'c'        // remove the third element, and return it
assert list == ['a','b','d','e','f','b','b','a']
```

### **set 操作**

```groovy
assert 'a' in ['a','b','c']             // returns true if an element belongs to the list
assert ['a','b','c'].contains('a')      // equivalent to the `contains` method in Java
assert [1,3,4].containsAll([1,4])       // `containsAll` will check that all elements are found

assert [1,2,3,3,3,3,4,5].count(3) == 4  // count the number of elements which have some value
assert [1,2,3,3,3,3,4,5].count {
    it%2==0                             // count the number of elements which match the predicate
} == 2

assert [1,2,4,6,8,10,12].intersect([1,3,6,9,12]) == [1,6,12]

assert [1,2,3].disjoint( [4,6,9] )
assert ![1,2,3].disjoint( [2,4,6] )
```

### **排序**

```groovy
assert [6, 3, 9, 2, 7, 1, 5].sort() == [1, 2, 3, 5, 6, 7, 9]

def list = ['abc', 'z', 'xyzuvw', 'Hello', '321']
assert list.sort {
    it.size()
} == ['z', 'abc', '321', 'Hello', 'xyzuvw']

def list2 = [7, 4, -6, -1, 11, 2, 3, -9, 5, -13]
assert list2.sort { a, b -> a == b ? 0 : Math.abs(a) < Math.abs(b) ? -1 : 1 } ==
        [-1, 2, 3, 4, 5, -6, 7, -9, 11, -13]

Comparator mc = { a, b -> a == b ? 0 : Math.abs(a) < Math.abs(b) ? -1 : 1 }

// JDK 8+ only
// list2.sort(mc)
// assert list2 == [-1, 2, 3, 4, 5, -6, 7, -9, 11, -13]

def list3 = [6, -3, 9, 2, -7, 1, 5]

Collections.sort(list3)
assert list3 == [-7, -3, 1, 2, 5, 6, 9]

Collections.sort(list3, mc)
assert list3 == [1, 2, -3, 5, 6, -7, 9]
```

**复制元素**

```groovy
assert [1, 2, 3] * 3 == [1, 2, 3, 1, 2, 3, 1, 2, 3]
assert [1, 2, 3].multiply(2) == [1, 2, 3, 1, 2, 3]
assert Collections.nCopies(3, 'b') == ['b', 'b', 'b']

// nCopies from the JDK has different semantics than multiply for lists
assert Collections.nCopies(2, [1, 2]) == [[1, 2], [1, 2]] //not [1,2,1,2]
```

## Maps

### **字面量Map**

```groovy
//定义
def map = [name: 'Gromit', likes: 'cheese', id: 1234]
#取值
assert map.get('name') == 'Gromit'
assert map.get('id') == 1234
assert map['name'] == 'Gromit'
assert map['id'] == 1234
# 判断类型
assert map instanceof java.util.Map
# 空Map
def emptyMap = [:]

assert emptyMap.size() == 0
emptyMap.put("foo", 5)
assert emptyMap.size() == 1
assert emptyMap.get("foo") == 5
```

>  Map keys are strings by default: `[a:1]` is equivalent to `['a':1]`

### **浅拷贝**

```groovy
def map = [
        simple : 123,
        complex: [a: 1, b: 2]
]
def map2 = map.clone()
assert map2.get('simple') == map.get('simple')
assert map2.get('complex') == map.get('complex')
map2.get('complex').put('c', 3)
assert map.get('complex').get('c') == 3
```

### 属性符号

>  Maps 也像 bean 一样，所以你可以使用属性符号来获取/设置 Map 内的项目，只要键是有效的 Groovy 标识符的字符串

```groovy
def map = [name: 'Gromit', likes: 'cheese', id: 1234]
assert map.name == 'Gromit'     // can be used instead of map.get('name')
assert map.id == 1234

def emptyMap = [:]
assert emptyMap.size() == 0
emptyMap.foo = 5
assert emptyMap.size() == 1
assert emptyMap.foo == 5
```

**字符串不能 与保留字相同**

```groovy
def map = [name: 'Gromit', likes: 'cheese', id: 1234]
assert map.class == null
assert map.get('class') == null
assert map.getClass() == LinkedHashMap // this is probably what you want

map = [1      : 'a',
       (true) : 'p',
       (false): 'q',
       (null) : 'x',
       'null' : 'z']
assert map.containsKey(1) // 1 is not an identifier so used as is
assert map.true == null
assert map.false == null
assert map.get(true) == 'p'
assert map.get(false) == 'q'
assert map.null == 'z'
assert map.get(null) == 'x'
```

### 迭代

```groovy
def map = [
        Bob  : 42,
        Alice: 54,
        Max  : 33
]

// `entry` is a map entry
map.each { entry ->
    println "Name: $entry.key Age: $entry.value"
}

// `entry` is a map entry, `i` the index in the map
map.eachWithIndex { entry, i ->
    println "$i - Name: $entry.key Age: $entry.value"
}

// Alternatively you can use key and value directly
map.each { key, value ->
    println "Name: $key Age: $value"
}

// Key, value and i as the index in the map
map.eachWithIndex { key, value, i ->
    println "$i - Name: $key Age: $value"
}
```

### 修改Map

```groovy
def defaults = [1: 'a', 2: 'b', 3: 'c', 4: 'd']
def overrides = [2: 'z', 5: 'x', 13: 'x']

def result = new LinkedHashMap(defaults)
result.put(15, 't')
result[17] = 'u'
result.putAll(overrides)
assert result == [1: 'a', 2: 'z', 3: 'c', 4: 'd', 5: 'x', 13: 'x', 15: 't', 17: 'u']
```

**清空**

```groovy
def m = [1:'a', 2:'b']
assert m.get(1) == 'a'
m.clear()
assert m == [:]
```

**注意：GString 的hashCode与String的HashCode值不一样**

```groovy
def key = 'some key'
def map = [:]
def gstringKey = "${key.toUpperCase()}"
map.put(gstringKey,'value')
assert map.get('SOME KEY') == null
```

**key视图，value视图**

```groovy
def map = [1:'a', 2:'b', 3:'c']

def entries = map.entrySet()
entries.each { entry ->
  assert entry.key in [1,2,3]
  assert entry.value in ['a','b','c']
}

def keys = map.keySet()
assert keys == [1,2,3] as Set
```

### 过滤和排序

```groovy
def people = [
    1: [name:'Bob', age: 32, gender: 'M'],
    2: [name:'Johnny', age: 36, gender: 'M'],
    3: [name:'Claire', age: 21, gender: 'F'],
    4: [name:'Amy', age: 54, gender:'F']
]

def bob = people.find { it.value.name == 'Bob' } // find a single entry
def females = people.findAll { it.value.gender == 'F' }

// both return entries, but you can use collect to retrieve the ages for example
def ageOfBob = bob.value.age
def agesOfFemales = females.collect {
    it.value.age
}

assert ageOfBob == 32
assert agesOfFemales == [21,54]

// but you could also use a key/pair value as the parameters of the closures
def agesOfMales = people.findAll { id, person ->
    person.gender == 'M'
}.collect { id, person ->
    person.age
}
assert agesOfMales == [32, 36]

// `every` returns true if all entries match the predicate
assert people.every { id, person ->
    person.age > 18
}

// `any` returns true if any entry matches the predicate

assert people.any { id, person ->
    person.age == 54
}
```

**分组**

```groovy
assert ['a', 7, 'b', [2, 3]].groupBy {
    it.class
} == [(String)   : ['a', 'b'],
      (Integer)  : [7],
      (ArrayList): [[2, 3]]
]

assert [
        [name: 'Clark', city: 'London'], [name: 'Sharma', city: 'London'],
        [name: 'Maradona', city: 'LA'], [name: 'Zhang', city: 'HK'],
        [name: 'Ali', city: 'HK'], [name: 'Liu', city: 'HK'],
].groupBy { it.city } == [
        London: [[name: 'Clark', city: 'London'],
                 [name: 'Sharma', city: 'London']],
        LA    : [[name: 'Maradona', city: 'LA']],
        HK    : [[name: 'Zhang', city: 'HK'],
                 [name: 'Ali', city: 'HK'],
                 [name: 'Liu', city: 'HK']],
]
```

## 序列

### 定义序列

> 用 `..` 符号定义的范围是包含的（即列表包含 from 和 to 值）。
>
> 用 ..< 符号定义的范围是半开的，它们包括第一个值但不包括最后一个值。

```groovy
// an inclusive range
def range = 5..8
assert range.size() == 4
assert range.get(2) == 7
assert range[2] == 7
assert range instanceof java.util.List
assert range.contains(5)
assert range.contains(8)

// lets use a half-open range
range = 5..<8
assert range.size() == 3
assert range.get(2) == 7
assert range[2] == 7
assert range instanceof java.util.List
assert range.contains(5)
assert !range.contains(8)

//get the end points of the range without using indexes
range = 1..10
assert range.from == 1
assert range.to == 10
```

**自定义序列**

只要实现 *Comparable* *next()* *previoust()* 就能创建序列

```groovy
// an inclusive range
def range = 'a'..'d'
assert range.size() == 4
assert range.get(2) == 'c'
assert range[2] == 'c'
assert range instanceof java.util.List
assert range.contains('a')
assert range.contains('d')
assert !range.contains('e')
```

### **迭代**

**for循环**

```groovy
for (i in 1..10) {
    println "Hello ${i}"
}
```

```groovy
(1..10).each { i ->
    println "Hello ${i}"
}
```

```groovy
switch (years) {
    case 1..10: interestRate = 0.076; break;
    case 11..25: interestRate = 0.052; break;
    default: interestRate = 0.037;
}
```



## 集合语法增强

### GPath支持

* dot 从集合中的每个元素上取 属性为a的对象，如果取不到则过滤
* *dot  从集合中的每个元素上取 属性为a的对象，如果取不到则赋值为空

由于对 list 和 map 的属性表示法的支持，Groovy 提供了语法糖，使得处理嵌套集合非常容易

```groovy
def listOfMaps = [['a': 11, 'b': 12], ['a': 21, 'b': 22]]
assert listOfMaps.a == [11, 21] //GPath notation
assert listOfMaps*.a == [11, 21] //spread dot notation

listOfMaps = [['a': 11, 'b': 12], ['a': 21, 'b': 22], null]
assert listOfMaps*.a == [11, 21, null] // caters for null values
assert listOfMaps*.a == listOfMaps.collect { it?.a } //equivalent notation
// But this will only collect non-null values
assert listOfMaps.a == [11,21]
```

### 集合展开

```groovy
# 类似于 调用   putAll,但是已有的同名key不会更新
assert [ 'z': 900,
         *: ['a': 100, 'b': 200], 'a': 300] == ['a': 300, 'b': 200, 'z': 900]
//spread map notation in map definition
assert [*: [3: 3, *: [5: 5]], 7: 7] == [3: 3, 5: 5, 7: 7]
//定义闭包返回Map
def f = { [1: 'u', 2: 'v', 3: 'w'] }
assert [*: f(), 10: 'zz'] == [1: 'u', 10: 'zz', 2: 'v', 3: 'w']
//spread map notation in function arguments
f = { map -> map.c }
assert f(*: ['a': 10, 'b': 20, 'c': 30], 'e': 50) == 30

f = { m, i, j, k -> [m, i, j, k] }
//using spread map notation with mixed unnamed and named arguments
assert f('e': 100, *[4, 5], *: ['a': 10, 'b': 20, 'c': 30], 6) ==
        [["e": 100, "b": 20, "c": 30, "a": 10], 4, 5, 6]
```

### *点运算符

“星点”运算符是一个快捷运算符，允许您调用集合中所有元素的方法或属性：

```groovy
assert [1, 3, 5] == ['a', 'few', 'words']*.size()

class Person {
    String name
    int age
}
def persons = [new Person(name:'Hugo', age:17), new Person(name:'Sandra',age:19)]
assert [17, 19] == persons*.age
```

### 使用下标运算符切片

您可以使用下标表达式对列表、数组、映射进行索引。有趣的是，在这种情况下，字符串被视为特殊类型的集合：

```groovy
def text = 'nice cheese gromit!'
def x = text[2]

assert x == 'c'
assert x.class == String

def sub = text[5..10]
assert sub == 'cheese'

def list = [10, 11, 12, 13]
def answer = list[2,3]
assert answer == [12,13]
```

**断点切片**

```groovy
list = 100..200
sub = list[1, 3, 20..25, 33]
assert sub == [101, 103, 120, 121, 122, 123, 124, 125, 133]
```

**下标运算符可用于更新现有集合（对于可变的集合类型）：**

```groovy
list = ['a','x','x','d']
list[1..2] = ['b','c']
assert list == ['a','b','c','d']
```

值得注意的是，允许使用负索引，以便更轻松地从集合的末尾提取：

```groovy
text = "nice cheese gromit!"
x = text[-1]
assert x == "!"
```

您可以使用负索引从列表、数组、字符串等的末尾开始计数。

```groovy
def name = text[-7..-2]
assert name == "gromit"
```

如果您使用反向索引（起始索引大于结束索引），则答案相反。

```groovy
text = "nice cheese gromit!"
name = text[3..1]
assert name == "eci"
```



# java1.8日期类

groovy-datetime 模块支持许多扩展以使用 Java 8 中引入的日期/时间 API。本文档将此 API 定义的数据类型称为“JSR 310 类型”。

## Formatting and parsing

| Method                      | Description                                                  | Example                                                      |
| :-------------------------- | :----------------------------------------------------------- | :----------------------------------------------------------- |
| `getDateString()`           | 针对于 LocalDate LocalDateTime 日期或时间类型转 string,      | `2018-03-10`                                                 |
|                             | `OffsetDateTime`, `DateTimeFormatter.ISO_OFFSET_DATE`        | `2018-03-10+04:00`                                           |
|                             | For `ZonedDateTime`, formats with DateTimeFormatter.ISO_LOCAL_DAT  and appends the `ZoneId` short name | `2018-03-10EST`                                              |
| `getDateTimeString()`       | For `LocalDateTime`, formats with DateTimeFormatter.ISO_LOCAL_DATE_TIME | `2018-03-10T20:30:45`                                        |
|                             | For `OffsetDateTime`, formats with DateTimeFormatter.ISO_OFFSET_DATE_TIME | `2018-03-10T20:30:45+04:00`                                  |
|                             | For `ZonedDateTime`, formats with DateTimeFormatter.ISO_LOCAL_DATE_TIME and appends the `ZoneId` short name | `2018-03-10T20:30:45EST`                                     |
| `getTimeString()`           | For `LocalTime` and `LocalDateTime`, formats with DateTimeFormatter.ISO_LOCAL_TIME | `20:30:45`                                                   |
|                             | For `OffsetTime` and `OffsetDateTime`, formats with DateTimeFormatter.ISO_OFFSET_TIME formatter | `20:30:45+04:00`                                             |
|                             | For `ZonedDateTime`, formats with DateTimeFormatter.ISO_LOCAL_TIME and appends the `ZoneId` short name | `20:30:45EST`                                                |
| `format(FormatStyle style)` | For `LocalTime` and `OffsetTime`, formats with DateTimeFormatter.ofLocalizedTime(style) | `4:30 AM` (with style `FormatStyle.SHORT`, e.g.)             |
|                             | For `LocalDate`, formats with DateTimeFormatter.ofLocalizedDate(style) | `Saturday, March 10, 2018` (with style `FormatStyle.FULL`, e.g.) |
|                             | For `LocalDateTime`, `OffsetDateTime`, and `ZonedDateTime` formats with DateTimeFormatter.ofLocalizedDateTime(style) | `Mar 10, 2019 4:30:45 AM` (with style `FormatStyle.MEDIUM`, e.g.) |
| `format(String pattern)`    | Formats with DateTimeFormatter.ofPattern(pattern)            | `03/10/2018` (with pattern ’MM/dd/yyyy', e.g.)               |

## 修改时间

### 加减

```groovy
def aprilFools = LocalDate.of(2018, Month.APRIL, 1)

def nextAprilFools = aprilFools + Period.ofDays(365) // add 365 days
assert nextAprilFools.year == 2019

def idesOfMarch = aprilFools - Period.ofDays(17) // subtract 17 days
assert idesOfMarch.dayOfMonth == 15
assert idesOfMarch.month == Month.MARCH
```

```groovy
def nextAprilFools = aprilFools + 365 // add 365 days
def idesOfMarch = aprilFools - 17 // subtract 17 days
```

这些整数的单位取决于 JSR 310 类型操作数

```groovy
//整形以秒为单位
def mars = LocalTime.of(12, 34, 56) // 12:34:56 pm

def thirtySecondsToMars = mars - 30 // go back 30 seconds
assert thirtySecondsToMars.second == 26
```

### 乘除

乘法运算符可用于将 Period 和 Duration 实例乘以整数值； 

除法运算符可用于将 Duration 实例除以整数值。

```groovy
def period = Period.ofMonths(1) * 2 // a 1-month period times 2
assert period.months == 2

def duration = Duration.ofSeconds(10) / 5// a 10-second duration divided by 5
assert duration.seconds == 2
```

**递增递减**

```groovy
def year = Year.of(2000)
--year // decrement by one year
assert year.value == 1999

def offsetTime = OffsetTime.of(0, 0, 0, 0, ZoneOffset.UTC) // 00:00:00.000 UTC
offsetTime++ // increment by one second
assert offsetTime.second == 1
```

### Negation

```groovy
def duration = Duration.ofSeconds(-15)
def negated = -duration
assert negated.seconds == 15
```

## 与日期/时间值交互

### **直接属性访问**

```groovy
def date = LocalDate.of(2018, Month.MARCH, 12)
assert date[ChronoField.YEAR] == 2018
assert date[ChronoField.MONTH_OF_YEAR] == Month.MARCH.value
assert date[ChronoField.DAY_OF_MONTH] == 12
assert date[ChronoField.DAY_OF_WEEK] == DayOfWeek.MONDAY.value

def period = Period.ofYears(2).withMonths(4).withDays(6)
assert period[ChronoUnit.YEARS] == 2
assert period[ChronoUnit.MONTHS] == 4
assert period[ChronoUnit.DAYS] == 6
```

### Ranges, `upto`, and `downto`

**时间范围迭代**

```groovy
def start = LocalDate.now()
def end = start + 6 // 6 days later
(start..end).each { date ->
    println date.dayOfWeek
}
```

**同上述**

```groovy
def start = LocalDate.now()
def end = start + 6 // 6 days later
start.upto(end) { next ->
    println next.dayOfWeek
}
```

upto、downto、ranges 的迭代单位与加减法的单位相同：LocalDate 一次迭代一天，YearMonth 迭代一个月，Year 迭代一年，其他时间迭代一秒。

这两种方法还支持可选的 TemporalUnit 参数来更改迭代单位。

```groovy
//按月迭代
def start = LocalDate.of(2018, Month.MARCH, 1)
def end = start + 1 // 1 day later

int iterationCount = 0
start.upto(end, ChronoUnit.MONTHS) { next ->
    println next
    ++iterationCount
}

assert iterationCount == 1
```

### Combining date/time values

左移运算符 (<<) 可用于将两个 JSR 310 类型组合成一个聚合类型。

例如，可以将 LocalDate 左移到 LocalTime 以生成复合 LocalDateTime 实例。

```groovy
MonthDay monthDay = Month.JUNE << 3 // June 3rd
LocalDate date = monthDay << Year.of(2015) // 3-Jun-2015
LocalDateTime dateTime = date << LocalTime.NOON // 3-Jun-2015 @ 12pm
OffsetDateTime offsetDateTime = dateTime << ZoneOffset.ofHours(-5) // 3-Jun-2015 @ 12pm UTC-5
```

左移运算符是自反的；

操作数的顺序无关紧要。

```groovy
def year = Year.of(2000)
def month = Month.DECEMBER

YearMonth a = year << month
YearMonth b = month << year
assert a == b
```

### 创建时间段和持续时间

右移运算符 (>>) 生成一个值，表示操作数之间的周期或持续时间。

对于 ChronoLocalDate、YearMonth 和 Year，该运算符生成一个 Period 实例：

```groovy
def newYears = LocalDate.of(2018, Month.JANUARY, 1)
def aprilFools = LocalDate.of(2018, Month.APRIL, 1)

def period = newYears >> aprilFools
assert period instanceof Period
assert period.months == 3
```

The operator produces a `Duration` for the time-aware JSR types:

```groovy
def duration = LocalTime.NOON >> (LocalTime.NOON + 30)
assert duration instanceof Duration
assert duration.seconds == 30
```

如果运算符左侧的值早于右侧的值，则结果为正。

如果左侧晚于右侧，则结果为负：

### 在传统和 JSR 310 类型之间转换

尽管 java.util 包中的 Date、Calendar 和 TimeZone 类型存在缺陷，但它们在 Java API 中非常常见（至少在 Java 8 之前的 API 中）。

为了适应此类 API 的使用，Groovy 提供了在 JSR 310 类型和遗留类型之间进行转换的方法。

大多数 JSR 类型都配备了 toDate() 和 toCalendar() 方法，用于转换为相对等效的 java.util.Date 和 java.util.Calendar 值。 

ZoneId 和 ZoneOffset 都被赋予了一个 toTimeZone() 方法来转换为 java.util.TimeZone。

```groovy
/ LocalDate to java.util.Date
def valentines = LocalDate.of(2018, Month.FEBRUARY, 14)
assert valentines.toDate().format('MMMM dd, yyyy') == 'February 14, 2018'

// LocalTime to java.util.Date
def noon = LocalTime.of(12, 0, 0)
assert noon.toDate().format('HH:mm:ss') == '12:00:00'

// ZoneId to java.util.TimeZone
def newYork = ZoneId.of('America/New_York')
assert newYork.toTimeZone() == TimeZone.getTimeZone('America/New_York')

// ZonedDateTime to java.util.Calendar
def valAtNoonInNY = ZonedDateTime.of(valentines, noon, newYork)
assert valAtNoonInNY.toCalendar().getTimeZone().toZoneId() == newYork
```



请注意，在转换为旧类型时：

* **纳秒值被截断为毫秒。**

  例如，一个 LocalTime 的 ChronoUnit.NANOS 值为 999,999,999 纳秒转换为 999 毫秒。

* 转换“本地”类型（LocalDate、LocalTime 和 LocalDateTime）时，返回的 Date 或 Calendar 的时区将是系统默认值

* 转换仅时间类型（LocalTime 或 OffsetTime）时，Date 或 Calendar 的年/月/日设置为当前日期。

* 转换仅日期类型（LocalDate）时，Date 或 Calendar 的时间值将被清除，即 00:00:00.000。

* 将 OffsetDateTime 转换为 Calendar 时，只有 ZoneOffset 的小时和分钟传送到相应的 TimeZone。

  幸运的是，非零秒的区域偏移很少见。

Groovy 向 Date 和 Calendar 添加了许多方法，用于转换为各种 JSR 310 类型：

```groovy
Date legacy = Date.parse('yyyy-MM-dd HH:mm:ss.SSS', '2010-04-03 10:30:58.999')

assert legacy.toLocalDate() == LocalDate.of(2010, 4, 3)
assert legacy.toLocalTime() == LocalTime.of(10, 30, 58, 999_000_000) // 999M ns = 999ms
assert legacy.toOffsetTime().hour == 10
assert legacy.toYear() == Year.of(2010)
assert legacy.toMonth() == Month.APRIL
assert legacy.toDayOfWeek() == DayOfWeek.SATURDAY
assert legacy.toMonthDay() == MonthDay.of(Month.APRIL, 3)
assert legacy.toYearMonth() == YearMonth.of(2010, Month.APRIL)
assert legacy.toLocalDateTime().year == 2010
assert legacy.toOffsetDateTime().dayOfMonth == 3
assert legacy.toZonedDateTime().zone == ZoneId.systemDefault()
```



# 有用的工具类

## ConfigSlurper

ConfigSlurper 是一个实用程序类，用于读取以 Groovy 脚本形式定义的配置文件。

就像 Java *.properties 文件的情况一样，ConfigSlurper 允许使用点符号。

但此外，它允许闭包范围的配置值和任意对象类型。



```groovy
def config = new ConfigSlurper().parse('''
//点表示
app.date = new Date()  
    app.age  = 42
    //使用闭包作用域替代点表示法
    app {                  
        name = "Test${42}"
    }
''')

assert config.app.date instanceof Date
assert config.app.age == 42
assert config.app.name == 'Test42'
```

从上面的例子中可以看出，parse 方法可用于检索 groovy.util.ConfigObject 实例。 

ConfigObject 是一个专门的 java.util.Map 实现，它要么返回配置的值，要么返回一个新的 ConfigObject 实例，但永远不会为空。



```groovy
def config = new ConfigSlurper().parse('''
    app.date = new Date()
    app.age  = 42
    app.name = "Test${42}"
''')

assert config.test != null   
```

如果点是配置变量名称的一部分，则可以使用单引号或双引号对其进行转义

```groovy
def config = new ConfigSlurper().parse('''
    app."person.age"  = 42
''')

assert config.app."person.age" == 42
```

**环境变量**

```groovy
def config = new ConfigSlurper('development').parse('''
  environments {
       development {
           app.port = 8080
       }

       test {
           app.port = 8082
       }

       production {
           app.port = 80
       }
  }
''')

assert config.app.port == 8080
```

环境方法是内置的，但 registerConditionalBlock 方法可用于注册除环境名称之外的其他方法名称。

```groovy
def slurper = new ConfigSlurper()
slurper.registerConditionalBlock('myProject', 'developers')   

def config = slurper.parse('''
  sendMail = true

  myProject {
       developers {
           sendMail = false
       }
  }
''')

assert !config.sendMail
```

出于 Java 集成的目的，可以使用 toProperties 方法将 ConfigObject 转换为 java.util.Properties 对象，该对象可能存储到 *.properties 文本文件中。

请注意，在将配置值添加到新创建的 Properties 实例期间，它们会转换为 String 实例。

```groovy
def config = new ConfigSlurper().parse('''
    app.date = new Date()
    app.age  = 42
    app {
        name = "Test${42}"
    }
''')

def properties = config.toProperties()

assert properties."app.date" instanceof String
assert properties."app.age" == '42'
assert properties."app.name" == 'Test42'
```

## Expando

Expando 类可用于创建动态可扩展对象。每个 Expando 对象都代表一个独立的、动态制作的实例，可以在运行时使用属性（或方法）进行扩展。

```groovy
def expando = new Expando()
expando.name = 'John'

assert expando.name == 'John'
```

当动态属性注册一个闭包代码块时会发生一种特殊情况。

一旦注册，就可以像调用方法一样调用它。

```groovy
def expando = new Expando()
expando.toString = { -> 'John' }
expando.say = { String s -> "John says: ${s}" }

assert expando as String == 'John'
assert expando.say('Hi') == 'John says: Hi'
```

## Observable list, map and set

当添加、删除或更改元素时，这些集合中的每一个都会触发 java.beans.PropertyChangeEvent 事件。

请注意，PropertyChangeEvent 不仅表示某个事件已发生，而且还保存有关属性名称和某个属性已更改为的旧/新值的信息。

```groovy
def event                                       
def listener = {
    if (it instanceof ObservableList.ElementEvent)  {  
        event = it
    }
} as PropertyChangeListener


def observable = [1, 2, 3] as ObservableList    
observable.addPropertyChangeListener(listener)  

observable.add 42                               

assert event instanceof ObservableList.ElementAddedEvent

def elementAddedEvent = event as ObservableList.ElementAddedEvent
assert elementAddedEvent.changeType == ObservableList.ChangeType.ADDED
assert elementAddedEvent.index == 3
assert elementAddedEvent.oldValue == null
assert elementAddedEvent.newValue == 42
```

```groovy
def event
def listener = {
    if (it instanceof ObservableList.ElementEvent)  {
        event = it
    }
} as PropertyChangeListener


def observable = [1, 2, 3] as ObservableList
observable.addPropertyChangeListener(listener)

observable.clear()

assert event instanceof ObservableList.ElementClearedEvent

def elementClearedEvent = event as ObservableList.ElementClearedEvent
assert elementClearedEvent.values == [1, 2, 3]
assert observable.size() == 0
```


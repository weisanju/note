# Arithmetic operators

## Normal arithmetic operators

The following binary arithmetic operators are available in Groovy:

| Operator | Purpose        | Remarks                                                      |
| :------- | :------------- | :----------------------------------------------------------- |
| `+`      | addition       |                                                              |
| `-`      | subtraction    |                                                              |
| `*`      | multiplication |                                                              |
| `/`      | division       | Use `intdiv()` for integer division, and see the section about [integer division](https://docs.groovy-lang.org/latest/html/documentation/core-syntax.html#integer_division) for more information on the return type of the division. |
| `%`      | remainder      |                                                              |
| `**`     | power          | See the section about [the power operation](https://docs.groovy-lang.org/latest/html/documentation/core-syntax.html#power_operator) for more information on the return type of the operation. |



## Unary operators

一元运算符

```.groovy
assert +3 == 3
assert -4 == 0 - 4

assert -(-1) == 1  
```

请注意使用括号包围表达式以将一元减号应用于该被包围的表达式。

就一元算术运算符而言，可用前缀和后缀表示法的 ++（增量）和 --（减量）运算符：

```groovy
def a = 2
def b = a++ * 3             

assert a == 3 && b == 6

def c = 3
def d = c-- * 2             

assert c == 2 && d == 6

def e = 1
def f = ++e + 3             

assert e == 2 && f == 5

def g = 4
def h = --g + 1             

assert g == 3 && h == 4
```

## Assignment arithmetic operators

赋值算术运算符

我们上面看到的二元算术运算符也有赋值形式：-`+=`

- `-=`
- `*=`
- `/=`
- `%=`
- `**=`

```groovy
def a = 4
a += 3

assert a == 7

def b = 5
b -= 3

assert b == 2

def c = 5
c *= 3

assert c == 15

def d = 10
d /= 2

assert d == 5

def e = 10
e %= 3

assert e == 1

def f = 3
f **= 2

assert f == 9
```

# Relational operators

关系运算符允许在对象之间进行比较，以了解两个对象是否相同或不同，或者一个对象是否大于、小于或等于另一个。

The following operators are available:

| Operator | Purpose                            |
| :------- | :--------------------------------- |
| `==`     | equal                              |
| `!=`     | different                          |
| `<`      | less than                          |
| `<=`     | less than or equal                 |
| `>`      | greater than                       |
| `>=`     | greater than or equal              |
| `===`    | identical (Since Groovy 3.0.0)     |
| `!==`    | not identical (Since Groovy 3.0.0) |

===` 和 `!==` 都被支持，它们分别与调用 `is()` 方法和否定调用 `is()` 方法相同。

# Logical operators

- `&&`: logical "and"
- `||`: logical "or"
- `!`: logical "not"

```groovy
assert !false           
assert true && true     
assert true || false    
```

## Precedence

逻辑“非”比逻辑“与”具有更高的优先级。

```groovy
assert (!false && false) == false   
```

逻辑“与”比逻辑“或”具有更高的优先级。

```groovy
assert true || true && false        
```

## Short-circuiting

```groovy
boolean checkIfCalled() {   
    called = true
}

called = false
true || checkIfCalled()
assert !called              

called = false
false || checkIfCalled()
assert called               

called = false
false && checkIfCalled()
assert !called              

called = false
true && checkIfCalled()
assert called               
```

# Bitwise and bit shift operators

## Bitwise operators

> 位运算

按位和位移运算符

- `&`: bitwise "and"
- `|`: bitwise "or"
- `^`: bitwise "xor" (exclusive "or")
- `~`: bitwise negation

位运算符可应用于字节、短整型、整型、长整型或大整数类型的参数。

如果参数之一是 BigInteger，则结果将是 BigInteger 类型；

否则，如果其中一个参数是 long，则结果将是 long 类型；

## Bit shift operators

移位运算

- `<<`: left shift
- `>>`: right shift
- `>>>`: right shift unsigned

```groovy
assert 12.equals(3 << 2)           
assert 24L.equals(3L << 3)         
assert 48G.equals(3G << 4)         

assert 4095 == -200 >>> 20
assert -1 == -200 >> 20
assert 2G == 5G >> 1
assert -3G == -5G >> 1
```

在 Groovy 中，位移运算符是可重载的，这意味着您可以为任何类型的对象定义这些运算符的行为。

# Conditional operators

## Not operator

“not”运算符用感叹号 (!) 表示，并反转底层布尔表达式的结果。特别是，可以将 not 运算符与 Groovy  truth 结合起来：

```groovy
assert (!true)    == false                      
assert (!'foo')   == false                      
assert (!'')      == true                       
```

## Ternary operator

三元运算符

```groovy
result = (string!=null && string.length()>0) ? 'Found' : 'Not found'
```

## Elvis operator

是三元运算符得缩写形式

```groovy
displayName = user.name ? user.name : 'Anonymous'   
displayName = user.name ?: 'Anonymous'     
```

Elvis 运算符的使用减少了代码的冗长性，并降低了重构时出错的风险，因为无需复制在条件和正返回值中都测试过的表达式。

## Elvis assignment operator



```groovy
import groovy.transform.ToString

@ToString
class Element {
    String name
    int atomicNumber
}

def he = new Element(name: 'Helium')
he.with {
    name = name ?: 'Hydrogen'   // existing Elvis operator
    atomicNumber ?= 2           // new Elvis assignment shorthand
}
assert he.toString() == 'Element(Helium, 2)'
```



# Object operators

## Safe navigation operator

**安全导航运算符**用于避免 NullPointerException。

通常，当您有一个对象的引用时，您可能需要在访问该对象的方法或属性之前验证它是否为空。

为了避免这种情况，安全导航运算符将简单地返回 null 而不是抛出异常，如下所示：

```groovy
def person = Person.find { it.id == 123 }    
def name = person?.name                      
assert name == null                          
```

### Direct field access operator

> 直接字段访问操作符.



**普通访问会调用Get方法**

```groovy
class User {
    public final String name                 
    User(String name) { this.name = name}
    String getName() { "Name: $name" }       
}
def user = new User('Bob')
assert user.name == 'Name: Bob'              
```

如果要检索字段而不是调用 getter，可以使用直接字段访问运算符：

```groovy
assert user.@name == 'Bob'                   
```

使用 .@ 强制使用字段而不是 getter

## Method pointer operator

方法指针运算符 (.&) 可用于在变量中存储对方法的引用，以便稍后调用：

```groovy
def str = 'example of method reference'            
def fun = str.&toUpperCase                         
def upper = fun()                                  
assert upper == str.toUpperCase()                  
```

使用方法指针有很多优点。

首先，这种方法指针的类型是 groovy.lang.Closure，所以它可以用在任何需要使用闭包的地方。

特别适合将现有的方法转换为策略模式的需要：



```groovy
def transform(List elements, Closure action) {                    
    def result = []
    elements.each {
        result << action(it)
    }
    result
}
String describe(Person p) {                                       
    "$p.name is $p.age"
}
def action = this.&describe                                       
def list = [
    new Person(name: 'Bob',   age: 42),
    new Person(name: 'Julia', age: 35)]                           
assert transform(list, action) == ['Bob is 42', 'Julia is 35']    
```

**方法指针由接收者和方法名绑定。**

参数是在运行时解析的，这意味着如果您有多个同名的方法，语法没有不同，只会在运行时解析要调用的适当方法：

```groovy
def doSomething(String str) { str.toUpperCase() }    
def doSomething(Integer x) { 2*x }                   
def reference = this.&doSomething                    
assert reference('foo') == 'FOO'                     
assert reference(123)   == 246                       
```

为了符合 Java 8 方法引用期望，在 Groovy 3 及更高版本中，您可以使用 new 作为方法名称来获取指向构造函数的方法指针：

```groovy
def foo  = BigInteger.&new
def fortyTwo = foo('42')
assert fortyTwo == 42G
```

同样在 Groovy 3 及更高版本中，您可以获得指向类的实例方法的方法指针。

此方法指针采用一个附加参数作为接收器实例来调用该方法：

```groovy
def instanceMethod = String.&toUpperCase
assert instanceMethod('foo') == 'FOO'
```

为了向后兼容，在这种情况下，任何碰巧具有正确调用参数的静态方法都将优先于实例方法

## Method reference operator

Groovy 3+ 中的 Parrot 解析器支持 Java 8+ 方法引用运算符。

方法引用运算符 (::) 可用于在需要函数式接口的上下文中引用方法或构造函数。

这与 Groovy 的方法指针运算符提供的功能有些重叠。



```groovy
import groovy.transform.CompileStatic
import static java.util.stream.Collectors.toList

@CompileStatic
void methodRefs() {
    assert 6G == [1G, 2G, 3G].stream().reduce(0G, BigInteger::add)                           

    assert [4G, 5G, 6G] == [1G, 2G, 3G].stream().map(3G::add).collect(toList())              

    assert [1G, 2G, 3G] == [1L, 2L, 3L].stream().map(BigInteger::valueOf).collect(toList())  

    assert [1G, 2G, 3G] == [1L, 2L, 3L].stream().map(3G::valueOf).collect(toList())          
}

methodRefs()
```

以下脚本显示了一些突出显示各种受支持的构造函数参考案例的示例：

```groovy
@CompileStatic
void constructorRefs() {
    assert [1, 2, 3] == ['1', '2', '3'].stream().map(Integer::new).collect(toList())  

    def result = [1, 2, 3].stream().toArray(Integer[]::new)                           
    assert result instanceof Integer[]
    assert result.toString() == '[1, 2, 3]'
}

constructorRefs()
```

# Regular expression operators

## Pattern operator

模式运算符 (~) 提供了一种创建 java.util.regex.Pattern 实例的简单方法：

```groovy
def p = ~/foo/
assert p instanceof Pattern
```

通常，您会在斜线字符串中找到带有表达式的模式运算符，但它可以与 Groovy 中的任何类型的字符串一起使用：

```groovy
p = ~'foo'                                                        
p = ~"foo"                                                        
p = ~$/dollar/slashy $ string/$                                   
p = ~"${pattern}"     
```

虽然您可以将大多数字符串形式与 Pattern、Find 和 Match 运算符一起使用，但我们建议在大多数情况下使用斜线字符串，以便不必记住其他需要的转义要求。

## Find operator

作为构建模式的替代方法，您可以使用 find 运算符 =~ 直接创建一个 java.util.regex.Matcher 实例：

```groovy
def text = "some text to match"
def m = text =~ /match/                                           
assert m instanceof Matcher                                       
//equivalent to calling if (!m.find(0))
if (!m) {                                                         
    throw new RuntimeException("Oops, text not found!")
}
```



## Match operator

匹配运算符 (==~) 是查找运算符的轻微变体，它不返回匹配器而是返回布尔值，并且需要输入字符串的严格匹配：

```groovy
m = text ==~ /match/                                              
assert m instanceof Boolean                                       
if (m) {                                                          
    throw new RuntimeException("Should not reach that point!")
}
```



## Comparing Find vs Match operators

通常，当模式涉及单个完全匹配时使用匹配运算符，否则查找运算符可能更有用。

```groovy
assert 'two words' ==~ /\S+\s+\S+/
assert 'two words' ==~ /^\S+\s+\S+$/         
assert !(' leading space' ==~ /\S+\s+\S+/)   

def m1 = 'two words' =~ /^\S+\s+\S+$/
assert m1.size() == 1                          
def m2 = 'now three words' =~ /^\S+\s+\S+$/    
assert m2.size() == 0                          
def m3 = 'now three words' =~ /\S+\s+\S+/
assert m3.size() == 1                          
assert m3[0] == 'now three'
def m4 = ' leading space' =~ /\S+\s+\S+/
assert m4.size() == 1                          
assert m4[0] == 'leading space'
def m5 = 'and with four words' =~ /\S+\s+\S+/
assert m5.size() == 2                          
assert m5[0] == 'and with'
assert m5[1] == 'four words'
```



# Other operators

## Spread operator

扩展点运算符 (*.)，通常缩写为扩展运算符，用于对聚合对象的所有 item 调用操作。

相当于在每一项上调用action，将结果收集到一个列表中：

```groovy
class Car {
    String make
    String model
}
def cars = [
       new Car(make: 'Peugeot', model: '508'),
       new Car(make: 'Renault', model: 'Clio')]       
def makes = cars*.make                                
assert makes == ['Peugeot', 'Renault']                
```

展开运算符是空安全的，这意味着如果集合的元素为空，它将返回空而不是抛出 NullPointerException：

```groovy
cars = [
   new Car(make: 'Peugeot', model: '508'),
   null,                                              
   new Car(make: 'Renault', model: 'Clio')]
assert cars*.make == ['Peugeot', null, 'Renault']     
assert null*.make == null       
```

展开运算符可用于任何实现 Iterable 接口的类：

```groovy
class Component {
    Long id
    String name
}
class CompositeObject implements Iterable<Component> {
    def components = [
        new Component(id: 1, name: 'Foo'),
        new Component(id: 2, name: 'Bar')]

    @Override
    Iterator<Component> iterator() {
        components.iterator()
    }
}
def composite = new CompositeObject()
assert composite*.id == [1,2]
assert composite*.name == ['Foo','Bar']
```

在处理本身包含聚合的数据结构聚合时，使用扩展点运算符的多次调用（此处为cars*.models*.name）：

```groovy
class Make {
    String name
    List<Model> models
}

@Canonical
class Model {
    String name
}

def cars = [
    new Make(name: 'Peugeot',
             models: [new Model('408'), new Model('508')]),
    new Make(name: 'Renault',
             models: [new Model('Clio'), new Model('Captur')])
]

def makes = cars*.name
assert makes == ['Peugeot', 'Renault']

def models = cars*.models*.name
assert models == [['408', '508'], ['Clio', 'Captur']]
assert models.sum() == ['408', '508', 'Clio', 'Captur'] // flatten one level
assert models.flatten() == ['408', '508', 'Clio', 'Captur'] // flatten all levels (one in this case)
```

```groovy
class Car {
    String make
    String model
}
def cars = [
   [
       new Car(make: 'Peugeot', model: '408'),
       new Car(make: 'Peugeot', model: '508')
   ], [
       new Car(make: 'Renault', model: 'Clio'),
       new Car(make: 'Renault', model: 'Captur')
   ]
]
def models = cars.collectNested{ it.model }
assert models == [['408', '508'], ['Clio', 'Captur']]
```

**Spreading method arguments**

在某些情况下，可以在列表中找到方法调用的参数，您需要适应方法参数。

在这种情况下，您可以使用扩展运算符来调用该方法。

```groovy
int function(int x, int y, int z) {
    x*y+z
}

def args = [4,5,6]
assert function(*args) == 26

args = [4]
assert function(*args,5,6) == 26
```

**Spread list elements**

```groovy
def items = [4,5]                      
def list = [1,2,3,*items,6]            
assert list == [1,2,3,4,5,6]           
```

**Spread map elements**

```groovy
def m1 = [c:3, d:4]                   
def map = [a:1, b:2, *:m1]            
assert map == [a:1, b:2, c:3, d:4]    
```

## Range operator

```groovy
def range = 0..5                                    
assert (0..5).collect() == [0, 1, 2, 3, 4, 5]       
assert (0..<5).collect() == [0, 1, 2, 3, 4]         
assert (0..5) instanceof List                       
assert (0..5).size() == 6                           
```

## Spaceship operator

飞船运算符 (<=>) 委托给 compareTo 方法：

```groovy
assert (1 <=> 1) == 0
assert (1 <=> 2) == -1
assert (2 <=> 1) == 1
assert ('a' <=> 'z') == -1
```



## Subscript operator

下标运算符是 getAt 或 putAt 的简写符号，具体取决于您是在赋值的左侧还是右侧找到它：

```groovy
def list = [0,1,2,3,4]
assert list[2] == 2                         
list[2] = 4                                 
assert list[0..2] == [0,1,4]                
list[0..2] = [6,6,6]                        
assert list == [6,6,6,3,4]                  
```

下标运算符与 getAt/putAt 的自定义实现相结合是解构对象的便捷方法：



```groovy
class User {
    Long id
    String name
    def getAt(int i) {                                             
        switch (i) {
            case 0: return id
            case 1: return name
        }
        throw new IllegalArgumentException("No such element $i")
    }
    void putAt(int i, def value) {                                 
        switch (i) {
            case 0: id = value; return
            case 1: name = value; return
        }
        throw new IllegalArgumentException("No such element $i")
    }
}
def user = new User(id: 1, name: 'Alex')                           
assert user[0] == 1                                                
assert user[1] == 'Alex'                                           
user[1] = 'Bob'                                                    
assert user.name == 'Bob'   
```

## Safe index operator

Groovy 3.0.0 引入了安全索引操作符，即 ?[]，类似于 ?.. 例如：

```groovy
String[] array = ['a', 'b']
assert 'b' == array?[1]      // get using normal array index
array?[1] = 'c'              // set using normal array index
assert 'c' == array?[1]

array = null
assert null == array?[1]     // return null for all index values
array?[1] = 'c'              // quietly ignore attempt to set value
assert null == array?[1]

def personInfo = [name: 'Daniel.Sun', location: 'Shanghai']
assert 'Daniel.Sun' == personInfo?['name']      // get using normal map index
personInfo?['name'] = 'sunlan'                  // set using normal map index
assert 'sunlan' == personInfo?['name']

personInfo = null
assert null == personInfo?['name']              // return null for all map values
personInfo?['name'] = 'sunlan'                  // quietly ignore attempt to set value
assert null == personInfo?['name']
```

## Membership operator

成员资格运算符 (in) 等效于调用 isCase 方法。在 List 的上下文中，它相当于调用 contains，如下例所示

```groovy
def list = ['Grace','Rob','Emmy']
assert ('Emmy' in list) 
```

equivalent to calling `list.contains('Emmy')` or `list.isCase('Emmy')`



## Identity operator

在 Groovy 中，使用 == 来测试相等性与在 Java 中使用相同的运算符不同。

在 Groovy 中，它调用的是 equals。

如果你想比较引用相等，你应该像下面的例子一样使用：

```groovy
def list1 = ['Groovy 1.8','Groovy 2.0','Groovy 2.3']        
def list2 = ['Groovy 1.8','Groovy 2.0','Groovy 2.3']        
assert list1 == list2                                       
assert !list1.is(list2)  
```

## Coercion operator

 强制操作符

强制运算符 (as) 是强制转换的变体。

强制将对象从一种类型转换为另一种类型，而它们不兼容赋值。

```groovy
Integer x = 123
String s = (String) x      
```

Integer 不能分配给 String，所以它会在运行时产生 ClassCastException,这可以通过使用强制来解决：

```groovy
Integer x = 123
//整数不可分配给字符串，但使用 as 会将其强制为字符串
String s = x as String      
```

当一个对象被强制转换为另一个对象时，除非目标类型与源类型相同，否则强制将返回一个新对象。

转换规则因源和目标类型而异，如果找不到转换规则，则转换可能会失败。

由于 asType 方法，可以实现自定义转换规则：

```groovy
class Identifiable {
    String name
}
class User {
    Long id
    String name
    def asType(Class target) {                                              
        if (target == Identifiable) {
            return new Identifiable(name: name)
        }
        throw new ClassCastException("User cannot be coerced into $target")
    }
}
def u = new User(name: 'Xavier')                                            
def p = u as Identifiable                                                   
assert p instanceof Identifiable                                            
assert !(p instanceof User)             
```



## Diamond operator

菱形运算符 (<>) 是一种语法糖运算符，用于支持与 Java 7 中同名运算符的兼容性。它用于指示应从声明中推断出泛型类型：

```groovy
List<String> strings = new LinkedList<>()
```

在动态 Groovy 中，这是完全未使用的。

在静态类型检查的 Groovy 中，它也是可选的，因为无论该运算符是否存在，Groovy 类型检查器都会执行类型推断。

## Call operator

调用运算符 () 用于隐式调用名为 call 的方法。

对于定义调用方法的任何对象，您可以省略 .call 部分并使用调用运算符代替：

```groovy
class MyCallable {
    int call(int x) {           
        2*x
    }
}

def mc = new MyCallable()
assert mc.call(2) == 4          
assert mc(2) == 4               
```

# Operator precedence

he table below lists all groovy operators in order of precedence.

| Level | Operator(s)                                                  | Name(s)                                                      |
| :---- | :----------------------------------------------------------- | :----------------------------------------------------------- |
| 1     | `new`  `()`                                                  | object creation, explicit parentheses                        |
|       | `()`  `{}`  `[]`                                             | method call, closure, literal list/map                       |
|       | `.`  `.&`  `.@`                                              | member access, method closure, field/attribute access        |
|       | `?.`  `*`  `*.`  `*:`                                        | safe dereferencing, spread, spread-dot, spread-map           |
|       | `~`  `!`  `(type)`                                           | bitwise negate/pattern, not, typecast                        |
|       | `[]`  `?[]`  `++`  `--`                                      | list/map/array (safe) index, post inc/decrement              |
| 2     | `**`                                                         | power                                                        |
| 3     | `++`  `--`  `+`  `-`                                         | pre inc/decrement, unary plus, unary minus                   |
| 4     | `*`  `/`  `%`                                                | multiply, div, remainder                                     |
| 5     | `+`  `-`                                                     | addition, subtraction                                        |
| 6     | `<<`  `>>`  `>>>`  `..`  `..<`                               | left/right (unsigned) shift, inclusive/exclusive range       |
| 7     | `<`  `<=`  `>`  `>=`  `in`  `!in`  `instanceof`  `!instanceof`  `as` | less/greater than/or equal, in, not in, instanceof, not instanceof, type coercion |
| 8     | `==`  `!=`  `<=>`  `===`  `!==`                              | equals, not equals, compare to, identical to, not identical to |
|       | `=~`  `==~`                                                  | regex find, regex match                                      |
| 9     | `&`                                                          | binary/bitwise and                                           |
| 10    | `^`                                                          | binary/bitwise xor                                           |
| 11    | `|`                                                          | binary/bitwise or                                            |
| 12    | `&&`                                                         | logical and                                                  |
| 13    | `||`                                                         | logical or                                                   |
| 14    | `? :`                                                        | ternary conditional                                          |
|       | `?:`                                                         | elvis operator                                               |
| 15    | `=`  `**=`  `*=`  `/=`  `%=`  `+=`  `-=`  `<<=`  `>>=`  `>>>=`  `&=`  `^=`  `|=`   `?=` | various assignments                                          |



# Operator overloading

Groovy 允许您重载各种运算符，以便它们可以与您自己的类一起使用。

```groovy
class Bucket {
    int size

    Bucket(int size) { this.size = size }

    Bucket plus(Bucket other) {                     
        return new Bucket(this.size + other.size)
    }
}
```

Just by implementing the `plus()` method, the `Bucket` class can now be used with the `+` operator like so:

```groovy
def b1 = new Bucket(4)
def b2 = new Bucket(11)
assert (b1 + b2).size == 15    
```

所有（非比较器）Groovy 运算符都有相应的方法，您可以在自己的类中实现该方法。

唯一的要求是您的方法是公共的，具有正确的名称，并且具有正确数量的参数。

参数类型取决于您希望在运算符右侧支持什么类型。



以下是运算符及其相应方法的完整列表： 

| Operator | Method        | Operator   | Method                  |
| :------- | :------------ | :--------- | :---------------------- |
| `+`      | a.plus(b)     | `a[b]`     | a.getAt(b)              |
| `-`      | a.minus(b)    | `a[b] = c` | a.putAt(b, c)           |
| `*`      | a.multiply(b) | `a in b`   | b.isCase(a)             |
| `/`      | a.div(b)      | `<<`       | a.leftShift(b)          |
| `%`      | a.mod(b)      | `>>`       | a.rightShift(b)         |
| `**`     | a.power(b)    | `>>>`      | a.rightShiftUnsigned(b) |
| `|`      | a.or(b)       | `++`       | a.next()                |
| `&`      | a.and(b)      | `--`       | a.previous()            |
| `^`      | a.xor(b)      | `+a`       | a.positive()            |
| `as`     | a.asType(b)   | `-a`       | a.negative()            |
| `a()`    | a.call()      | `~a`       | a.bitwiseNegate()       |


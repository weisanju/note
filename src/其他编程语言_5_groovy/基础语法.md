# 注释

## 单行注释

```groovy
// a standalone single line comment
```

## 多行注释

```groovy
/* a standalone multiline comment
   spanning two lines */
```

## 文档注释

```groovy
/**
 * A Class description
 */
```

Groovydoc 遵循与 Java 自己的 Javadoc 相同的约定。因此，您将能够使用与 Javadoc 相同的标签。

## 运行时文档保留

另外，Groovy 从 3.0.0 开始支持 Runtime Groovydoc，即可以在运行时保留 Groovydoc。

默认情况下禁用运行时 Groovydoc。可以通过添加 JVM 选项 -Dgroovy.attach.runtime.groovydoc=true 来启用它

Runtime Groovydoc 以 /**@ 开头并以 */ 结尾，例如：

```groovy
/**@
 * Some class groovydoc for Foo
 */
class Foo {
    /**@
     * Some method groovydoc for bar
     */
    void bar() {
    }
}
```

```groovy
assert Foo.class.groovydoc.content.contains('Some class groovydoc for Foo') 
assert Foo.class.getMethod('bar', new Class[0]).groovydoc.content.contains('Some method groovydoc for bar') 
```

## Shebang line

除了单行注释之外，还有一个特殊的行注释，通常被称为 UNIX 系统理解的 shebang 行，它允许直接从命令行运行脚本，前提是您已经安装了 Groovy 发行版并且 groovy 命令可用于路径。

```groovy
#!/usr/bin/env groovy
println "Hello from the shebang line"
```

 The `#` character must be the first character of the file. Any indentation would yield a compilation error.

# Keywords

The following list represents all the keywords of the Groovy language:

|        |         |            |            |
| ------ | ------- | ---------- | ---------- |
| as     | assert  | break      | case       |
| catch  | class   | const      | continue   |
| def    | default | do         | else       |
| enum   | extends | false      | finally    |
| for    | goto    | if         | implements |
| import | in      | instanceof | interface  |
| new    | null    | package    | return     |
| super  | switch  | this       | throw      |
| throws | trait   | true       | try        |
| var    | while   |            |            |

# 标识符

## 普通标识符

标识符以字母、美元或下划线开头。它们不能以数字开头。

字母可以在以下范围内： 'a' 到 'z'（小写 ascii 字母） 'A' 到 'Z'（大写 ascii 字母） '\u00C0' 到 '\u00D6' '\u00D8' 到 '\u00F6 

' '\u00F8' 到 '\u00FF' '\u0100' 到 '\uFFFE'

## 带引号的标识符

带引号的标识符出现在带点表达式的点之后。

例如，person.name 表达式的 name 部分可以用 person."name" 或 person.'name' 引用。

当某些标识符包含 Java 语言规范禁止的非法字符时，但在引用时 Groovy 允许这些字符。

例如，破折号、空格、感叹号等字符。

```groovy
def firstname = "Homer"
map."Simpson-${firstname}" = "Homer Simpson"

assert map.'Simpson-Homer' == "Homer Simpson"
```

# Strings

文本文字以称为字符串的字符链的形式表示。 Groovy 允许您实例化 java.lang.String 对象以及 GStrings (groovy.lang.GString)，它们在其他编程语言中也称为内插字符串。

## Single-quoted string

单引号字符串是纯 java.lang.String 并且不支持插值。

## 字符串连接

```groovy
assert 'ab' == 'a' + 'b'
```

## Triple-single-quoted string

```groovy
'''a triple-single-quoted string'''
```

三重单引号字符串是纯 java.lang.String 并且不支持插值。

三重单引号字符串可以跨越多行。

字符串的内容可以跨越行边界，无需将字符串分成几部分，也无需连接或换行转义字符：

```groovy
def aMultilineString = '''line one
line two
line three'''
```

**反斜杠去除开头的换行符**

```groovy
def strippedFirstNewline = '''\
line one
line two
line three
'''

assert !strippedFirstNewline.startsWith('\n')
```

## Escaping special characters

```groovy
'an escaped single quote: \' needs a backslash'
```

```groovy
'an escaped escape character: \\ needs a double backslash'
```

Some special characters also use the backslash as escape character:

| Escape sequence | Character                                                    |
| :-------------- | :----------------------------------------------------------- |
| \b              | backspace                                                    |
| \f              | formfeed                                                     |
| \n              | newline                                                      |
| \r              | carriage return                                              |
| \s              | single space                                                 |
| \t              | tabulation                                                   |
| \\              | backslash                                                    |
| \'              | single quote within a single-quoted string (and optional for triple-single-quoted and double-quoted strings) |
| \"              | double quote within a double-quoted string (and optional for triple-double-quoted and single-quoted strings) |

您可以使用 unicode 转义序列：反斜杠，后跟“u”，然后是 4 个十六进制数字。

```groovy
'The Euro currency symbol: \u20AC'
```

## Double-quoted string

如果没有插值表达式，双引号字符串就是普通的 java.lang.String，如果存在插值，则是 groovy.lang.GString 实例。

#### String interpolation

除了单引号和三重单引号字符串外，任何 Groovy 表达式都可以插入到所有字符串文字中。

占位符使用 `${}`

**使用本地变量**

```groovy
def name = 'Guillaume' // a plain string
def greeting = "Hello ${name}"

assert greeting.toString() == 'Hello Guillaume'
```

**表达式**

```groovy
def sum = "The sum of 2 and 3 equals ${2 + 3}"
assert sum.toString() == 'The sum of 2 and 3 equals 5'
```

不仅在 ${} 占位符之间允许表达式，而且语句也是如此。

但是，语句的值只是 null。

因此，如果在该占位符中插入了多个语句，插入的表达式最终应该返回 值

```groovy
"The sum of 1 and 2 is equal to ${def a = 1; def b = 2; a + b}"
```

除了 ${} 占位符之外，我们还可以使用单独的 $ 符号作为 点表达式的前缀

但是只能是 属性访问

```groovy
def person = [name: 'Guillaume', age: 36]
assert "$person.name is $person.age years old" == 'Guillaume is 36 years old'
```

### 插入闭包表达式的特例

到目前为止，我们已经看到我们可以在 ${} 占位符中插入任意表达式，但是闭包表达式有一个特殊的情况和符号。

当占位符包含一个箭头 ${→} 时，表达式实际上是一个闭包表达式 — 你可以把它想象成一个前面加了一个美元的闭包：

```groovy
//无参闭包
def sParameterLessClosure = "1 + 2 == ${-> 3}" 
assert sParameterLessClosure == '1 + 2 == 3'

//单参数闭包，w 为stringWriter
def sOneParamClosure = "1 + 2 == ${ w -> w << 3}" 
assert sOneParamClosure == '1 + 2 == 3'
```

**闭包的好处：惰性求值**

```groovy
def number = 1 
def eagerGString = "value == ${number}"
def lazyGString = "value == ${ -> number }"

assert eagerGString == "value == 1" 
assert lazyGString ==  "value == 1" 

number = 2 
assert eagerGString == "value == 1" 
assert lazyGString ==  "value == 2" 
```

### Interoperability with Java

```groovy
String takeString(String message) {         
    assert message instanceof String        
    return message

}

//定义 GString
def message = "The message is ${'hello'}"   
assert message instanceof GString           

//调用 申明为String的方法，会隐式的调用 GString的 toString
def result = takeString(message)     
assert result instanceof String
assert result == 'The message is hello'
```

### GString and String hashCodes

尽管可以使用内插字符串代替纯 Java 字符串，但它们与字符串的不同之处在于：它们的哈希码不同。

纯 Java 字符串是不可变的，而 GString 的结果字符串表示形式可能会有所不同，具体取决于其内插值。

即使对于相同的结果字符串，GStrings 和 Strings 也没有相同的 hashCode。

```groovy
assert "one: ${1}".hashCode() != "one: 1".hashCode()
```

GString 和具有不同 hashCode 值的字符串，应避免使用 GString 作为 Map 键，尤其是当我们尝试使用 String 而不是 GString 检索关联值时。

```groovy
def key = "a"
def m = ["${key}": "letter ${key}"]     

assert m["a"] == null                   
```



## Triple-double-quoted string

三重双引号字符串的行为类似于双引号字符串，另外还有它们是多行的，就像三重单引号字符串一样。

双引号和单引号都不需要在三重双引号字符串中转义。

```groovy
def name = 'Groovy'
def template = """
    Dear Mr ${name},

    You're the winner of the lottery!

    Yours sincerly,

    Dave
"""

assert template.toString().contains('Groovy')
```

## Slashy string

除了通常的带引号的字符串之外，Groovy 还提供斜线字符串，它使用 / 作为开始和结束分隔符。

斜线字符串对于定义正则表达式和模式特别有用，因为不需要转义反斜线。

```groovy
def fooPattern = /.*foo.*/
assert fooPattern == '.*foo.*'
```

```groovy
def escapeSlash = /The character \/ is a forward slash/
assert escapeSlash == 'The character / is a forward slash'
```

Slashy strings are multiline:

```groovy
def multilineSlashy = /one
    two
    three/

assert multilineSlashy.contains('\n')
```

Slashy 字符串可以被认为是定义 GString 的另一种方式，但具有不同的转义规则。

因此，它们支持插值：

```groovy
def color = 'blue'
def interpolatedSlashy = /a ${color} car/

assert interpolatedSlashy == 'a blue car'
```

### Special cases

空斜杠字符串不能用双正斜杠表示，因为 Groovy 解析器将其理解为行注释。

这就是为什么以下断言实际上不会通过编译的原因，因为它看起来像一个未终止的语句：

```groovy
assert '' == //
```

## Dollar slashy string

Dollar 斜线字符串是多行 GString，以开头的 $/ 和结尾的 /$ 分隔。

转义字符是美元符号，它可以转义另一个美元或正斜杠。

但是美元和正斜杠都不需要转义，除非要转义以 GString 占位符序列开头的字符串子序列的美元，或者如果您需要转义以结束美元斜杠字符串分隔符开头的序列

```groovy
def name = "Guillaume"
def date = "April, 1st"

def dollarSlashy = $/
    Hello $name,
    today we're ${date}.

    $ dollar sign
    $$ escaped dollar sign
    \ backslash
    / forward slash
    $/ escaped forward slash
    $$$/ escaped opening dollar slashy
    $/$$ escaped closing dollar slashy
/$

assert [
    'Guillaume',
    'April, 1st',
    '$ dollar sign',
    '$ escaped dollar sign',
    '\\ backslash',
    '/ forward slash',
    '/ escaped forward slash',
    '$/ escaped opening dollar slashy',
    '/$ escaped closing dollar slashy'
].every { dollarSlashy.contains(it) }
```

它的创建是为了克服斜线字符串转义规则的一些限制。

当它的转义规则适合你的字符串内容时使用它（通常如果它有一些你不想转义的斜线）。

## 字符串汇总表

| String name          | String syntax | Interpolated | Multiline | Escape character |
| -------------------- | ------------- | ------------ | --------- | ---------------- |
| Single-quoted        | `'…'`         |              |           | `\`              |
| Triple-single-quoted | `'''…'''`     |              | 1         | `\`              |
| Double-quoted        | `"…"`         | 1            |           | `\`              |
| Triple-double-quoted | `"""…"""`     | 1            | 1         | `\`              |
| Slashy               | `/…/`         | 1            | 1         | `\`              |
| Dollar slashy        | `$/…/$`       | 1            | 1         | `$`              |

## Characters

与 Java 不同，Groovy 没有明确的字符字面量。

但是，您可以通过三种不同的方式明确地将 Groovy 字符串设为实际字符：

```groovy
char c1 = 'A' 
assert c1 instanceof Character

def c2 = 'B' as char 
assert c2 instanceof Character

def c3 = (char)'C' 
assert c3 instanceof Character
```

# Numbers

Groovy 支持不同类型的整数文字和十进制文字，由 Java 的常用 Number 类型支持。

## Integral literals

The integral literal types are the same as in Java:

- `byte`
- `char`
- `short`
- `int`
- `long`
- `java.lang.BigInteger`

```groovy
// primitive types
byte  b = 1
char  c = 2
short s = 3
int   i = 4
long  l = 5

// infinite precision
BigInteger bi =  6
```

如果您使用 def 关键字使用可选类型，则整数的类型会有所不同：它将适应可以容纳该数字的类型的容量。

**对于正数：**

```groovy
def a = 1
assert a instanceof Integer

// Integer.MAX_VALUE
def b = 2147483647
assert b instanceof Integer

// Integer.MAX_VALUE + 1
def c = 2147483648
assert c instanceof Long

// Long.MAX_VALUE
def d = 9223372036854775807
assert d instanceof Long

// Long.MAX_VALUE + 1
def e = 9223372036854775808
assert e instanceof BigInteger
```

**对于负数**

```groovy
def na = -1
assert na instanceof Integer

// Integer.MIN_VALUE
def nb = -2147483648
assert nb instanceof Integer

// Integer.MIN_VALUE - 1
def nc = -2147483649
assert nc instanceof Long

// Long.MIN_VALUE
def nd = -9223372036854775808
assert nd instanceof Long

// Long.MIN_VALUE - 1
def ne = -9223372036854775809
assert ne instanceof BigInteger
```

**几种非十进制**

### Binary literal

二进制数字以 0b 前缀开头：

```groovy
int xInt = 0b10101111
assert xInt == 175

short xShort = 0b11001001
assert xShort == 201 as short

byte xByte = 0b11
assert xByte == 3 as byte

long xLong = 0b101101101101
assert xLong == 2925l

BigInteger xBigInteger = 0b111100100001
assert xBigInteger == 3873g

int xNegativeInt = -0b10101111
assert xNegativeInt == -175
```

### Octal literal

八进制数以“0”后跟八进制数字的典型格式指定。

```groovy
int xInt = 077
assert xInt == 63

short xShort = 011
assert xShort == 9 as short

byte xByte = 032
assert xByte == 26 as byte

long xLong = 0246
assert xLong == 166l

BigInteger xBigInteger = 01111
assert xBigInteger == 585g

int xNegativeInt = -077
assert xNegativeInt == -63
```

### Hexadecimal literal

十六进制数字以 0x 后跟十六进制数字的典型格式指定。

```groovy
nt xInt = 0x77
assert xInt == 119

short xShort = 0xaa
assert xShort == 170 as short

byte xByte = 0x3a
assert xByte == 58 as byte

long xLong = 0xffff
assert xLong == 65535l

BigInteger xBigInteger = 0xaaaa
assert xBigInteger == 43690g

Double xDouble = new Double('0x1.0p0')
assert xDouble == 1.0d

int xNegativeInt = -0x77
assert xNegativeInt == -119
```

## Decimal literals

**小数类型**

- `float`
- `double`
- `java.lang.BigDecimal`

```groovy
// primitive types
float  f = 1.234
double d = 2.345

// infinite precision
BigDecimal bd =  3.456
```

小数可以使用指数，用 e 或 E 指数字母，后跟可选符号，以及表示指数的整数：

```groovy
assert 1e3  ==  1_000.0
assert 2E4  == 20_000.0
assert 3e+1 ==     30.0
assert 4E-2 ==      0.04
assert 5e-1 ==      0.5
```

为了方便地计算精确的十进制数，**Groovy 选择 java.lang.BigDecimal 作为其十进制数类型。**

此外，支持 float 和 double，但需要明确的类型声明、类型强制或后缀。

即使 BigDecimal 是十进制数的默认值，在以 float 或 double 作为参数类型的方法或闭包中也接受此类文字。

十进制数不能用二进制、八进制或十六进制表示。

## Underscore in literals

写长的文字数字时，眼睛很难弄清楚一些数字是如何组合在一起的，例如千组、单词等。通过允许您在数字文字中放置下划线，更容易发现这些组：

```groovy
long creditCardNumber = 1234_5678_9012_3456L
long socialSecurityNumbers = 999_99_9999L
double monetaryAmount = 12_345_132.12
long hexBytes = 0xFF_EC_DE_5E
long hexWords = 0xFFEC_DE5E
long maxLong = 0x7fff_ffff_ffff_ffffL
long alsoMaxLong = 9_223_372_036_854_775_807L
long bytes = 0b11010010_01101001_10010100_10010010
```

## Number type suffixes

我们可以通过给一个大写或小写的后缀（见下表）来强制一个数字（包括二进制、八进制和十六进制）具有特定的类型。

| Type       | Suffix     |
| :--------- | :--------- |
| BigInteger | `G` or `g` |
| Long       | `L` or `l` |
| Integer    | `I` or `i` |
| BigDecimal | `G` or `g` |
| Double     | `D` or `d` |
| Float      | `F` or `f` |

```groovy
assert 42I == new Integer('42')
assert 42i == new Integer('42') // lowercase i more readable
assert 123L == new Long("123") // uppercase L more readable
assert 2147483648 == new Long('2147483648') // Long type used, value too large for an Integer
assert 456G == new BigInteger('456')
assert 456g == new BigInteger('456')
assert 123.45 == new BigDecimal('123.45') // default BigDecimal type used
assert 1.200065D == new Double('1.200065')
assert 1.234F == new Float('1.234')
assert 1.23E23D == new Double('1.23E23')
assert 0b1111L.class == Long // binary
assert 0xFFi.class == Integer // hexadecimal
assert 034G.class == BigInteger // octal
```

## Math operations

- binary operations between `byte`, `char`, `short` and `int` result in `int`
- binary operations involving `long` with `byte`, `char`, `short` and `int` result in `long`
- binary operations involving `BigInteger` and any other integral type result in `BigInteger`
- binary operations involving `BigDecimal` with `byte`, `char`, `short`, `int` and `BigInteger` result in `BigDecimal`
- binary operations between `float`, `double` and `BigDecimal` result in `double`
- binary operations between two `BigDecimal` result in `BigDecimal`



### table summarizes those rules:

|                | byte | char | short | int  | long | BigInteger | float  | double | BigDecimal |
| :------------- | :--- | :--- | :---- | :--- | :--- | :--------- | :----- | :----- | :--------- |
| **byte**       | int  | int  | int   | int  | long | BigInteger | double | double | BigDecimal |
| **char**       |      | int  | int   | int  | long | BigInteger | double | double | BigDecimal |
| **short**      |      |      | int   | int  | long | BigInteger | double | double | BigDecimal |
| **int**        |      |      |       | int  | long | BigInteger | double | double | BigDecimal |
| **long**       |      |      |       |      | long | BigInteger | double | double | BigDecimal |
| **BigInteger** |      |      |       |      |      | BigInteger | double | double | BigDecimal |
| **float**      |      |      |       |      |      |            | double | double | double     |
| **double**     |      |      |       |      |      |            |        | double | double     |
| **BigDecimal** |      |      |       |      |      |            |        |        | BigDecimal |

由于 Groovy 的运算符重载，通常的算术运算符可以与 BigInteger 和 BigDecimal 一起使用，这与在 Java 中必须使用显式方法对这些数字进行运算不同。

### 除法运算符的情况

除法运算符 /（和 /= 用于除法和赋值）如果操作数是浮点数或双精度数，则产生双精度结果，否则产生 BigDecimal 结果（当两个操作数都是整数类型 short、char、byte、int、 

long、BigInteger 或 BigDecimal）。

`BigDecimal` division is performed with the `divide()` method if the division is exact (i.e. yielding a result that can be represented within the bounds of the same precision and scale), or using a `MathContext` with a [precision](http://docs.oracle.com/javase/7/docs/api/java/math/BigDecimal.html#precision()) of the maximum of the two operands' precision plus an extra precision of 10, and a [scale](http://docs.oracle.com/javase/7/docs/api/java/math/BigDecimal.html#scale()) of the maximum of 10 and the maximum of the operands' scale.

### 幂运算符

幂运算由 **  运算符表示，有两个参数：基数和指数。

幂运算的结果取决于其操作数和运算结果（特别是如果结果可以表示为整数值）。

Groovy 的幂运算使用以下规则来确定结果类型：

* 如果指数是 小数
  * 如果结果可以表示为整数，则返回整数
  * 否则 如果结果可以表示为 Long，则返回 Long
  * otherwise return a `Double`
* 如果指数是 整数
  * 如果指数严格为负，则如果结果值适合该类型，则返回 Integer、Long 或 Double
  * 如果指数为正数或零
    * 如果基数是 BigDecimal，则返回 BigDecimal 结果值
    * 如果基数是 BigInteger，则返回 BigInteger 结果值
    * 如果基数是一个 Integer，则如果结果值 装得下它，则返回一个Integer，否则返回一个 BigInteger
    * 如果基数是 Long，则如果结果值适合，则返回 Long，否则返回 BigInteger

我们可以用几个例子来说明这些规则：

```groovy
// base and exponent are ints and the result can be represented by an Integer
assert    2    **   3    instanceof Integer    //  8
assert   10    **   9    instanceof Integer    //  1_000_000_000

// the base is a long, so fit the result in a Long
// (although it could have fit in an Integer)
assert    5L   **   2    instanceof Long       //  25

// the result can't be represented as an Integer or Long, so return a BigInteger
assert  100    **  10    instanceof BigInteger //  10e20
assert 1234    ** 123    instanceof BigInteger //  170515806212727042875...

// the base is a BigDecimal and the exponent a negative int
// but the result can be represented as an Integer
assert    0.5  **  -2    instanceof Integer    //  4

// the base is an int, and the exponent a negative float
// but again, the result can be represented as an Integer
assert    1    **  -0.3f instanceof Integer    //  1

// the base is an int, and the exponent a negative int
// but the result will be calculated as a Double
// (both base and exponent are actually converted to doubles)
assert   10    **  -1    instanceof Double     //  0.1

// the base is a BigDecimal, and the exponent is an int, so return a BigDecimal
assert    1.2  **  10    instanceof BigDecimal //  6.1917364224

// the base is a float or double, and the exponent is an int
// but the result can only be represented as a Double value
assert    3.4f **   5    instanceof Double     //  454.35430372146965
assert    5.6d **   2    instanceof Double     //  31.359999999999996

// the exponent is a decimal value
// and the result can only be represented as a Double value
assert    7.8  **   1.9  instanceof Double     //  49.542708423868476
assert    2    **   0.1f instanceof Double     //  1.0717734636432956
```

# Booleans

Boolean 是一种特殊的数据类型，用于表示真值：true 和 false。

将此数据类型用于跟踪真/假条件的简单标志。

```groovy
def myBooleanVariable = true
boolean untypedBooleanVar = false
booleanField = true
```



其他集合类详见 [基础语法](基础语法.md)

